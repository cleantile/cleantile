include "../lib/license"
include "../lib/blade-warning"
:coffeescript
  window.CleanTile ?= {}
  ###*
  Describes a partion between the two sides of a [`<cleantile-split>`][].  Used in [`<cleantile-drag>`][].
  
  Custom drag elements **must** extend the DragBehavior to be recognized by [`<cleantile-split>`][], and be sent data as
  it changes.
  
      <dom-module id="narrow-drag">
        <template>
        </template>
        <script>
          Polymer({
            is: "narrow-drag",
            behaviors: [CleanTile.DragBehavior],
            ready: function() {
              // this.name can be preset via `<wrapper-file-preview name="File: index.html">`
              if(!this.name) {
                this.name = "File Preview";
              }
            }
          })
        </ script >
      </dom-module>
  
  [`<cleantile-split>`]: #cleantile-split
  [`<cleantile-drag>`]: #cleantile-drag
  
  @polymerBehavior
  ###
  CleanTile.DragBehavior =
    properties:
      
      ###*
      Determines if the elements in the parent split are nested above each other (`vertical`) or next to each other
      (`horizontal`).
      
      Value automatically fetched from parent `cleantile-split`.
      ###
      direction:
        type: String
        value: "horizontal"
        reflectToAttribute: yes
      
      ###*
      Sets the division point between the two elements of the parent split.  Can contain:
      
      - `val` (`<cleantile-split>.boundary`)
      - `min` (`<cleantile-split>.min-boundary`)
      - `max` (`<cleantile-split>.max-boundary`)
      
      The parent split will be notified of changes to any of the fields in `boundary`.
      ###
      boundary:
        type: Object
        value: {}
        observer: "_boundaryChanged"
    
    ###*
    Returns the parent split, if one exists.
    @return {HTMLElement} the `<cleantile-split>`, if one exists.
    ###
    split: ->
      try
        parent = Polymer.dom(@).parentNode
        while parent.tagName.toLowerCase() is "cleantile-split"
          parent = parent.parentNode
        return parent
      catch e
        return
    
    ###*
    If the parent split has been initialized, fetch property values.
    ###
    ready: ->
      return unless (split = @split()) and split.get
      @set "direction", split.get "direction"
      @set "boundary",
        val: split.get "boundary"
        min: split.get "min-boundary"
        max: split.get "max-boundary"
    
    ###*
    Keeps the parent `<cleantile-split>` in sync with the properties in `{boundary}`.
    ###
    _boundaryChanged: (boundary) ->
      return unless (split = @split()) and split.set
      if boundary?.val? then split.set "boundary", boundary.val
      if boundary?.min? then split.set "min-boundary", boundary.min
      if boundary?.max? then split.set "max-boundary", boundary.max
